plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.17'
}

group = 'org.cataclysm'
version = '1.0'

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    // Paper dev bundle
    paperweight.paperDevBundle("1.21.8-R0.1-SNAPSHOT")

    // Aikar Command Framework
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    // Reflections
    implementation "org.reflections:reflections:0.10.2"

    // Lombok
    compileOnly "org.projectlombok:lombok:1.18.34"
    annotationProcessor "org.projectlombok:lombok:1.18.34"
    testCompileOnly "org.projectlombok:lombok:1.18.34"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.34"
}

tasks {
    runServer {
        minecraftVersion("1.21.8")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}